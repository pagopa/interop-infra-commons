data "aws_security_group" "vpn_clients" {
  id = var.vpn_clients_security_group_id
}

# -- Data Sources for Merging YAML Values --
# Terraform data for deep merging YAML values ​​using yq
# Runs the merge-values.sh script, which uses yq to merge YAML files
resource "terraform_data" "merge_argocd_values" {
  count = local.should_merge_custom_values ? 1 : 0

  # triggers_replace forces the provisioner to run when input files change (both creation and update)
  # When one of the files changes, Terraform destroys and recreates the terraform_data resource by rerunning the provisioner
  triggers_replace = {
    base_file_hash     = md5(local.default_values_file)
    override_file_hash = var.argocd_custom_values != null ? filemd5(var.argocd_custom_values) : ""
  }

  input = {
    base_file_content = local.default_values_file
    override_file     = var.argocd_custom_values
  }

  provisioner "local-exec" {
    command = "${path.module}/scripts/merge-values.sh > ${local.merged_file_name}"

    environment = {
      MODULE_PATH   = path.module
      BASE_FILE     = base64encode(self.input.base_file_content)
      OVERRIDE_FILE = self.input.override_file
    }
  }
}

# Data source to read the file generated by terraform_data
data "local_file" "merged_values" {
  count    = local.should_merge_custom_values ? 1 : 0
  filename = local.merged_file_name

  depends_on = [terraform_data.merge_argocd_values]
}

